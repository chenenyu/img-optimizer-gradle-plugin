apply plugin: 'maven-publish'
apply plugin: 'signing'

def GROUP = 'com.chenenyu'
def ARTIFACT_ID = 'img-optimizer'
def VERSION = '1.2.0'

group = GROUP
version = VERSION

// task to generate source.jar
task sourcesJar(type: Jar) {
    group 'build'
    archiveClassifier.set('sources')
    from sourceSets.main.java.srcDirs
}

javadoc {
    options.encoding = 'UTF-8'
    failOnError false
}

// task to generate javadoc.jar
task javadocJar(type: Jar, dependsOn: javadoc) {
    group 'build'
    from javadoc.getDestinationDir()
    archiveClassifier.set('javadoc')
}

//artifacts {
//    archives jar
//    archives javadocJar
//    archives sourcesJar
//}

afterEvaluate {
    publishing {
        repositories {
            maven {
                // https://central.sonatype.org/publish/publish-gradle/
                def releasesRepoUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
                def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

                credentials {
                    username = ossrhUsername
                    password = ossrhPassword
                }
            }
        }

        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact javadocJar

                groupId GROUP
                artifactId ARTIFACT_ID
                version VERSION
                pom {
                    packaging 'jar'
                    name = "${GROUP}:${ARTIFACT_ID}"
                    description = 'A gradle plugin for optimizing PNG.'
                    url = 'https://github.com/chenenyu/img-optimizer-gradle-plugin'
                    scm {
                        url = 'https://github.com/chenenyu/img-optimizer-gradle-plugin'
                        connection = 'scm:git@github.com:chenenyu/img-optimizer-gradle-plugin.git'
                        developerConnection = 'scm:git@github.com:chenenyu/img-optimizer-gradle-plugin.git'
                    }
                    licenses {
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'chenenyu'
                            name = 'chenenyu'
                            url = 'https://github.com/chenenyu'
                        }
                    }
                }
            }
        }
    }

    signing {
        sign publishing.publications.release
    }
}
